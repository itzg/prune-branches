import io.github.itzg.ghrel.GithubReleaserPlugin

plugins {
    id 'java'
    id 'application'
    // https://plugins.gradle.org/plugin/io.freefair.lombok
    id 'io.freefair.lombok' version '8.3'
    // https://github.com/qoomon/gradle-git-versioning-plugin
    id 'me.qoomon.git-versioning' version '6.4.2'
    id 'io.github.itzg.github-releaser' version '0.1.1'
}

apply plugin: GithubReleaserPlugin

group 'me.itzg'
description 'Prunes local git branches without remote branch'

version = '0.0.0-SNAPSHOT'
gitVersioning.apply {
    refs {
        branch('.+') {
            version = '${ref}-SNAPSHOT'
        }
        tag('(?<version>.*)') {
            version = '${ref.version}'
        }
    }
    rev {
        version = '${commit}'
    }
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

application {
    mainClass = 'me.itzg.app.PruneCommand'
}

tasks.distTar {
    compression = Compression.GZIP
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.apache:6.6.0.202305301015-r'
    implementation 'info.picocli:picocli:4.7.5'
    implementation 'ch.qos.logback:logback-classic:1.4.11'
    implementation 'org.slf4j:slf4j-api:2.0.7'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'

    annotationProcessor 'info.picocli:picocli-codegen:4.7.5'
}

test {
    useJUnitPlatform()
}

githubReleaser {
    project {
        license = 'MIT'
        homepage = 'https://github.com/itzg/prune-branches'
    }
}