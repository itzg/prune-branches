plugins {
    id 'java'
    id 'application'
    // https://plugins.gradle.org/plugin/io.freefair.lombok
    id 'io.freefair.lombok' version '9.0.0'
    // https://github.com/qoomon/gradle-git-versioning-plugin
    id 'me.qoomon.git-versioning' version '6.4.4'
    id 'io.github.itzg.github-releaser' version '0.2.1'
}

group 'me.itzg'
description 'Prunes local git branches without remote branch'

version = '0.0.0-SNAPSHOT'
gitVersioning.apply {
    refs {
        branch('.+') {
            version = '${ref}-SNAPSHOT'
        }
        tag('(?<version>.*)') {
            version = '${ref.version}'
        }
    }
    rev {
        version = '${commit}'
    }
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

application {
    mainClass = 'me.itzg.app.PruneCommand'
}

tasks.distTar {
    compression = Compression.GZIP
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.apache:7.4.0.202509020913-r'
    implementation 'info.picocli:picocli:4.7.7'
    implementation 'ch.qos.logback:logback-classic:1.5.19'
    implementation 'org.slf4j:slf4j-api:2.0.17'

    // https://junit.org/junit5/docs/current/user-guide/#running-tests-build-gradle-bom
    testImplementation(platform("org.junit:junit-bom:6.0.0"))
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    annotationProcessor 'info.picocli:picocli-codegen:4.7.7'
}

test {
    useJUnitPlatform()
}

githubReleaser {
    project {
        license = 'MIT'
        homepage = 'https://github.com/itzg/prune-branches'
    }
}