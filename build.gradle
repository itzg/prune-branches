plugins {
    id 'java'
    id 'application'
    // https://plugins.gradle.org/plugin/io.freefair.lombok
    id 'io.freefair.lombok' version '6.3.0'
    // For managing incrementing of project.version and tagging
    // https://github.com/netzwerg/gradle-release-plugin
    id 'ch.netzwerg.release' version '1.2.3'
    // https://jreleaser.org/guide/latest/index.html
    id 'org.jreleaser' version '0.9.0'
}

group 'me.itzg'
description 'Prunes local git branches without remote branch'

repositories {
    mavenCentral()
}

compileJava {
    options.release = 8
}

application {
    mainClass = 'me.itzg.app.PruneCommand'
}

tasks.distTar {
    compression = Compression.GZIP
}

// For bumping version.txt and tagging with ch.netzwerg.release
release {
    push = project.findProperty("pushReleaseTag") ?: false
}

jreleaser {
    project {
        website = 'https://github.com/itzg/prune-branches'
        docsUrl = 'https://github.com/itzg/prune-branches'
        authors = ['Geoff Bourne']
        tags = ['cli']
        license = 'MIT'
        extraProperties.put('inceptionYear', '2021')
    }
    distributions {
        app {
            artifacts {
                artifact {
                    path = "build/distributions/{{projectName}}-{{projectVersion}}.zip"
                }
            }
        }
    }
    release {
        github {
            owner = 'itzg'
            branch = 'master'
            prerelease {
                pattern = '.*-SNAPSHOT'
            }
            changelog {
                formatted = 'ALWAYS'
                preset = 'conventional-commits'
                labeler {
                    label = 'release'
                    title = '^Prepare next release '
                }
                labeler {
                    label = 'release'
                    title = '^Release '
                }
                category {
                    title = 'Internal'
                    labels = ['release']
                }
                hide {
                    categories = ['Internal']
                }
                contributors {
                    format = '- {{contributorName}}'
                }
            }
        }
    }
    packagers {
        scoop {
            active = 'RELEASE'
            bucket {
                owner = 'itzg'
                name = 'scoop-bucket'
            }
        }
        brew {
            active = 'RELEASE'
            repoTap {
                owner = 'itzg'
                name = 'homebrew-tap'
            }
        }
    }
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.apache:5.13.0.202109080827-r'
    implementation 'info.picocli:picocli:4.6.2'
    implementation 'ch.qos.logback:logback-classic:1.2.7'
    implementation 'org.slf4j:slf4j-api:1.7.32'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    annotationProcessor 'info.picocli:picocli-codegen:4.6.2'
}

test {
    useJUnitPlatform()
}