plugins {
    id 'java'
    id 'application'
    id "io.freefair.lombok" version "6.2.0"
    // For managing incrementing of project.version and tagging
    // https://github.com/netzwerg/gradle-release-plugin
    id 'ch.netzwerg.release' version '1.2.3'
    // https://jreleaser.org/guide/latest/index.html
    id 'org.jreleaser' version '0.7.0'
}

group 'me.itzg'
description 'CLI tool that prunes local branches that no longer point to existing remote branches'

repositories {
    mavenCentral()
}

compileJava {
    options.release = 8
}

application {
    mainClass = 'me.itzg.app.PruneCommand'
}

tasks.distTar {
    compression = Compression.GZIP
}

// For bumping version.txt and tagging with ch.netzwerg.release
release {
    push = project.findProperty("pushReleaseTag") ?: false
}

jreleaser {
    project {
        website = 'https://github.com/itzg/prune-branches'
        docsUrl = 'https://github.com/itzg/prune-branches'
        authors = ['Geoff Bourne']
        tags = ['cli']
        license = 'MIT'
        extraProperties.put('inceptionYear', '2021')
    }
    release {
        github {
            //noinspection GroovyAccessibility
            owner = 'itzg'
            branch = 'master'
            prerelease {
                pattern = '.*-SNAPSHOT'
            }
            changelog {
                labeler {
                    label = 'ci'
                    title = '^ci:'
                }
                labeler {
                    label = 'test'
                    title = '^test'
                }
                category {
                    title = 'Internal'
                    labels = ['ci', 'test']
                }
                hide {
                    categories = ['Internal']
                }
            }
        }
    }
    packagers {
        scoop {
            active = 'RELEASE'
            bucket {
                owner = 'itzg'
                name = 'scoop-bucket'
            }
        }
/* This block currently fails with
   Cannot set the value of read-only property 'owner' for root project 'prune-branches' of type org.gradle.api.Project
        brew {
            active = 'RELEASE'
            tap {
                owner = 'itzg'
                name = 'homebrew-tap'
            }
        }
*/
    }
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.apache:5.13.0.202109080827-r'
    implementation 'info.picocli:picocli:4.6.1'
    implementation 'ch.qos.logback:logback-classic:1.2.6'
    implementation 'org.slf4j:slf4j-api:1.7.32'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    annotationProcessor 'info.picocli:picocli-codegen:4.6.1'
}

test {
    useJUnitPlatform()
}