import io.github.itzg.ghrel.GithubReleaserPlugin

plugins {
    id 'java'
    id 'application'
    // https://plugins.gradle.org/plugin/io.freefair.lombok
    id 'io.freefair.lombok' version '8.6'
    // https://github.com/qoomon/gradle-git-versioning-plugin
    id 'me.qoomon.git-versioning' version '6.4.3'
    id 'io.github.itzg.github-releaser' version '0.1.1'
}

apply plugin: GithubReleaserPlugin

group 'me.itzg'
description 'Prunes local git branches without remote branch'

version = '0.0.0-SNAPSHOT'
gitVersioning.apply {
    refs {
        branch('.+') {
            version = '${ref}-SNAPSHOT'
        }
        tag('(?<version>.*)') {
            version = '${ref.version}'
        }
    }
    rev {
        version = '${commit}'
    }
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

application {
    mainClass = 'me.itzg.app.PruneCommand'
}

tasks.distTar {
    compression = Compression.GZIP
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.apache:6.9.0.202403050737-r'
    implementation 'info.picocli:picocli:4.7.6'
    implementation 'ch.qos.logback:logback-classic:1.5.6'
    implementation 'org.slf4j:slf4j-api:2.0.13'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'

    annotationProcessor 'info.picocli:picocli-codegen:4.7.6'
}

test {
    useJUnitPlatform()
}

githubReleaser {
    project {
        license = 'MIT'
        homepage = 'https://github.com/itzg/prune-branches'
    }
}